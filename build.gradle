plugins {
    id 'fabric-loom' version '1.14.10'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

repositories {
    mavenCentral()
    //maven { url "https://jitpack.io" }
    maven { url "https://api.modrinth.com/maven" }
    maven { url "https://cursemaven.com/" }
}

loom {
    runs {
        datagen {
            inherit server
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=variantcraftingtables"

            runDir "build/datagen"
        }
    }
}

sourceSets {
    main {
        resources {
            srcDirs += [
                    'src/main/generated'
            ]
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"

    mappings loom.layered {
        mappings("net.fabricmc:yarn:${project.yarn_mappings}:v2")
        mappings(file("mappings/mappings-fix.tiny"))
    }

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // Ecologics & Cloth Config
    modRuntimeOnly("curse.maven:cloth-config-348521:5623418")
    modRuntimeOnly("curse.maven:ecologics-570463:6751318")

    //BOP
    modRuntimeOnly("curse.maven:biomes-o-plenty-220318:7251967")
    modRuntimeOnly("curse.maven:glitchcore-955399:5660741")
    modRuntimeOnly("curse.maven:terrablender-565956:6054948")

    // NightConfig (provides com.electronwill.nightconfig.core.UnmodifiableConfig)
    implementation("com.electronwill.night-config:core:3.8.3")
    implementation("com.electronwill.night-config:toml:3.8.3")

    // TypeTools (provides net.jodah.typetools.TypeResolver)
    implementation("net.jodah:typetools:0.6.3")

    //Vanilla Backport
    modRuntimeOnly("curse.maven:vanillabackport-417430:7227051")
    modRuntimeOnly("curse.maven:platform-997634:7227586")
}


base {
    archivesName = project.archives_base_name
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    from("LICENSE") {
        rename { "${it}_${base.archivesName.get()}" }
    }
}

/* ============================
   MAVEN PUBLISH
   ============================ */
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }
    repositories {
    }
}

